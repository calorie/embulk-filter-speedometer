plugins {
    id "java"
    id "java-library"
    id "org.embulk.embulk-plugins" version "0.5.5"
}

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    provided
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = "org.embulk"
version = "0.4.0"

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.43"
    compileOnly "org.embulk:embulk-spi:0.10.43"

    implementation("org.embulk:embulk-util-config:0.3.2") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.31.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.43.
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-core:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    implementation "javax.validation:validation-api:1.1.0.Final"

    implementation "org.embulk:embulk-util-timestamp:0.2.1"

    implementation "com.google.guava:guava:31.1-jre"
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.slf4j:slf4j-simple:2.0.7'

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.jmockit:jmockit:1.49"
    testImplementation "org.embulk:embulk-core:0.10.43"
    testImplementation "org.embulk:embulk-deps:0.10.43"
    testImplementation "org.embulk:embulk-junit4:0.10.43"
}

embulkPlugin {
    mainClass = "org.embulk.filter.speedometer.SpeedometerFilterPlugin"
    category = "filter"
    type = "speedometer"
}

javadoc {
    options {
        locale = "en_US"
        encoding = "UTF-8"
    }
}

jar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

gem {
    authors = [ "hata" ]
    email = [ "hiroki.ata@gmail.com" ]
    summary = "Logging the message of processed bytes and throughput periodically."
    homepage = "https://github.com/hata/embulk-filter-speedometer"
    licenses = [ "MIT" ]

    from rootProject.file("LICENSE")
}

gemPush {
    host = "https://rubygems.org"
}

test {
    doFirst {
        jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }}"
    }
}
